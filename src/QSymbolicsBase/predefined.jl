##
# Pure States
##

abstract type SpecialKet <: Symbolic{AbstractKet} end
istree(::SpecialKet) = false
basis(x::SpecialKet) = x.basis
Base.show(io::IO, x::SpecialKet) = print(io, "|$(symbollabel(x))âŸ©")

@withmetadata struct XBasisState <: SpecialKet
    idx::Int
    basis::Basis
end
symbollabel(x::XBasisState) = "X$(num_to_sub(x.idx))"

@withmetadata struct YBasisState <: SpecialKet
    idx::Int
    basis::Basis
end
symbollabel(x::YBasisState) = "Y$(num_to_sub(x.idx))"

@withmetadata struct ZBasisState <: SpecialKet
    idx::Int
    basis::Basis
end
symbollabel(x::ZBasisState) = "Z$(num_to_sub(x.idx))"

@withmetadata struct FockBasisState <: SpecialKet
    idx::Int
    basis::Basis
end
symbollabel(x::FockBasisState) = "$(x.idx)"

@withmetadata struct DiscreteCoherentState <: SpecialKet
    alpha::Number # TODO parameterize
    basis::Basis
end
symbollabel(x::DiscreteCoherentState) = "$(x.alpha)"

@withmetadata struct ContinuousCoherentState <: SpecialKet
    alpha::Number # TODO parameterize
    basis::Basis
end
symbollabel(x::ContinuousCoherentState) = "$(x.alpha)"

@withmetadata struct MomentumEigenState <: SpecialKet
    p::Number # TODO parameterize
    basis::Basis
end
symbollabel(x::MomentumEigenState) = "Î´â‚š($(x.p))"

@withmetadata struct PositionEigenState <: SpecialKet
    x::Float64 # TODO parameterize
    basis::Basis
end
symbollabel(x::PositionEigenState) = "Î´â‚“($(x.x))"

const qubit_basis = SpinBasis(1//2)
"""Basis state of ÏƒË£"""
const X1 = const Xâ‚ = const Lp = const Lâ‚Š = XBasisState(1, qubit_basis)
"""Basis state of ÏƒË£"""
const X2 = const Xâ‚‚ = const Lm = const Lâ‚‹ = XBasisState(2, qubit_basis)
"""Basis state of ÏƒÊ¸"""
const Y1 = const Yâ‚ = const Lpi = const Lâ‚Šáµ¢ = YBasisState(1, qubit_basis)
"""Basis state of ÏƒÊ¸"""
const Y2 = const Yâ‚‚ = const Lmi = const Lâ‚‹áµ¢ = YBasisState(2, qubit_basis)
"""Basis state of Ïƒá¶»"""
const Z1 = const Zâ‚ = const L0 = const Lâ‚€ = ZBasisState(1, qubit_basis)
"""Basis state of Ïƒá¶»"""
const Z2 = const Zâ‚‚ = const L1 = const Lâ‚ = ZBasisState(2, qubit_basis)

const inf_fock_basis = FockBasis(Inf,0.)
"""Vacuum basis state of n"""
const vac = const Fâ‚€ = const F0 = FockBasisState(0,inf_fock_basis)
"""Single photon basis state of n"""
const Fâ‚ = const F1 = FockBasisState(1,inf_fock_basis)


##
# Gates and Operators on qubits
##

abstract type AbstractSingleQubitOp <: Symbolic{AbstractOperator} end
abstract type AbstractTwoQubitOp <: Symbolic{AbstractOperator} end
abstract type AbstractSingleQubitGate <: AbstractSingleQubitOp end # TODO maybe an IsUnitaryTrait is a better choice
abstract type AbstractTwoQubitGate <: AbstractTwoQubitOp end
istree(::AbstractSingleQubitGate) = false
istree(::AbstractTwoQubitGate) = false
basis(::AbstractSingleQubitGate) = qubit_basis
basis(::AbstractTwoQubitGate) = qubit_basisâŠ—qubit_basis
Base.show(io::IO, x::AbstractSingleQubitOp) = print(io, "$(symbollabel(x))")
Base.show(io::IO, x::AbstractTwoQubitOp) = print(io, "$(symbollabel(x))")


@withmetadata struct OperatorEmbedding <: Symbolic{AbstractOperator}
    gate::Symbolic{AbstractOperator} # TODO parameterize
    indices::Vector{Int}
    basis::Basis
end
istree(::OperatorEmbedding) = true

@withmetadata struct XGate <: AbstractSingleQubitGate end
eigvecs(g::XGate) = [X1,X2]
symbollabel(::XGate) = "X"
@withmetadata struct YGate <: AbstractSingleQubitGate end
eigvecs(g::YGate) = [Y1,Y2]
symbollabel(::YGate) = "Y"
@withmetadata struct ZGate <: AbstractSingleQubitGate end
eigvecs(g::ZGate) = [Z1,Z2]
symbollabel(::ZGate) = "Z"
@withmetadata struct PauliM <: AbstractSingleQubitGate end
symbollabel(::PauliM) = "Ïƒâ‚‹"
@withmetadata struct PauliP <: AbstractSingleQubitGate end
symbollabel(::PauliP) = "Ïƒâ‚Š"
@withmetadata struct HGate <: AbstractSingleQubitGate end
symbollabel(::HGate) = "H"
@withmetadata struct CNOTGate <: AbstractTwoQubitGate end
symbollabel(::CNOTGate) = "CNOT"
@withmetadata struct CPHASEGate <: AbstractTwoQubitGate end
symbollabel(::CPHASEGate) = "CPHASE"

"""Pauli X operator, also available as the constant `ÏƒË£`"""
const X = const ÏƒË£ = XGate()
"""Pauli Y operator, also available as the constant `ÏƒÊ¸`"""
const Y = const ÏƒÊ¸ = YGate()
"""Pauli Z operator, also available as the constant `Ïƒá¶»`"""
const Z = const Ïƒá¶» = ZGate()
"""Pauli "minus" operator, also available as the constant `Ïƒâ‚‹`"""
const Pm = const Ïƒâ‚‹ = PauliM()
"""Pauli "plus" operator, also available as the constant `Ïƒâ‚Š`"""
const Pp = const Ïƒâ‚Š = PauliP()
"""Hadamard gate"""
const H = HGate()
"""CNOT gate"""
const CNOT = CNOTGate()
"""CPHASE gate"""
const CPHASE = CPHASEGate()

##
# Gates and Operators on harmonic oscillators
##

abstract type AbstractSingleBosonOp <: Symbolic{AbstractOperator} end
abstract type AbstractSingleBosonGate <: AbstractSingleBosonOp end # TODO maybe an IsUnitaryTrait is a better choice
istree(::AbstractSingleBosonGate) = false
basis(::AbstractSingleBosonGate) = inf_fock_basis

@withmetadata struct NumberOp <: AbstractSingleBosonOp end
symbollabel(::NumberOp) = "n"
@withmetadata struct CreateOp <: AbstractSingleBosonOp end
symbollabel(::CreateOp) = "aâ€ "
@withmetadata struct DestroyOp <: AbstractSingleBosonOp end
symbollabel(::DestroyOp) = "a"

"""Number operator, also available as the constant `nÌ‚`"""
const N = const nÌ‚ = NumberOp()
"""Creation operator, also available as the constant `aÌ‚êœ›` - there is no unicode dagger superscript, so we use the uparrow"""
const Create = const aÌ‚êœ› = CreateOp()
"""Annihilation operator, also available as the constant `aÌ‚`"""
const Destroy = const aÌ‚ = DestroyOp()

##
# Other special or useful objects
##

"""Projector for a given ket

```jldoctest
julia> SProjector(X1âŠ—X2)
ğ[|Xâ‚âŸ©|Xâ‚‚âŸ©]

julia> express(SProjector(X2))
Operator(dim=2x2)
  basis: Spin(1/2)
  0.5+0.0im  -0.5-0.0im
 -0.5+0.0im   0.5+0.0im
```"""
@withmetadata struct SProjector <: Symbolic{AbstractOperator}
    ket::Symbolic{AbstractKet} # TODO parameterize
end
istree(::SProjector) = true
arguments(x::SProjector) = [x.ket]
operation(x::SProjector) = projector
exprhead(x::SProjector) = :projector
projector(x::Symbolic{AbstractKet}) = SProjector(x)
basis(x::SProjector) = basis(x.ket)
function Base.show(io::IO, x::SProjector)
    print(io,"ğ[")
    print(io,x.ket)
    print(io,"]")
end

"""Dagger a Ket into Bra."""
@withmetadata struct SDagger <: Symbolic{AbstractBra}
    ket::Symbolic{AbstractKet}
end
istree(::SDagger) = true
arguments(x::SDagger) = [x.ket]
operation(x::SDagger) = dagger
exprhead(x::SDagger) = :dagger
dagger(x::Symbolic{AbstractKet}) = SDagger(x)
dagger(x::SScaledKet) = SScaledBra(x.coeff, dagger(x.obj))
dagger(x::SAddKet) = SAddBra(Dict(dagger(k)=>v for (k,v) in pairs(x.dict)))
basis(x::SDagger) = basis(x.ket)
function Base.show(io::IO, x::SDagger)
    print(io,x.ket)
    print(io,"â€ ")
end
symbollabel(x::SDagger) = symbollabel(x.ket)

"""Completely depolarized state

```jldoctest
julia> MixedState(X1âŠ—X2)
ğ•„

julia> express(MixedState(X1âŠ—X2))
Operator(dim=4x4)
  basis: [Spin(1/2) âŠ— Spin(1/2)]sparse([1, 2, 3, 4], [1, 2, 3, 4], ComplexF64[0.25 + 0.0im, 0.25 + 0.0im, 0.25 + 0.0im, 0.25 + 0.0im], 4, 4)

  express(MixedState(X1âŠ—X2), CliffordRepr())
  Rank 0 stabilizer

  â”â”â”â”
  + X_
  + _X
  â”â”â”â”

  â”â”â”â”
  + Z_
  + _Z
```"""
@withmetadata struct MixedState <: Symbolic{AbstractOperator}
    basis::Basis # From QuantumOpticsBase # TODO make QuantumInterface
end
MixedState(x::Symbolic{AbstractKet}) = MixedState(basis(x))
MixedState(x::Symbolic{AbstractOperator}) = MixedState(basis(x))
istree(::MixedState) = false
basis(x::MixedState) = x.basis
symbollabel(x::MixedState) = "ğ•„"

"""The identity operator for a given basis

```judoctest
julia> IdentityOp(X1âŠ—X2)
ğ•€

julia> express(IdentityOp(Z2))
Operator(dim=2x2)
  basis: Spin(1/2)sparse([1, 2], [1, 2], ComplexF64[1.0 + 0.0im, 1.0 + 0.0im], 2, 2)
```"""
@withmetadata struct IdentityOp <: Symbolic{AbstractOperator}
    basis::Basis # From QuantumOpticsBase # TODO make QuantumInterface
end
IdentityOp(x::Symbolic{AbstractKet}) = IdentityOp(basis(x))
IdentityOp(x::Symbolic{AbstractOperator}) = IdentityOp(basis(x))
istree(::IdentityOp) = false
basis(x::IdentityOp) = x.basis
symbollabel(x::IdentityOp) = "ğ•€"
